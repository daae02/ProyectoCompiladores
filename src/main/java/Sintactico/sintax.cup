package Sintactico;

import java_cup.runtime.Symbol;
import Semantico.PilaSemantica;
import Semantico.Traductor;
import Semantico.TablaSimbolos;

parser code
{:
    private Symbol s;
    public void syntax_error(Symbol s){
        this.s = s;
        System.out.println("Error Sintáctico en la Línea " + (s.left) +
        " Columna "+s.right+ ". No se esperaba este componente: " +s.value+".");
    }
    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{ 
        System.out.println("Error síntactico irrecuperable en la Línea " + 
        (s.left)+ " Columna "+s.right+". Componente " + s.value + 
        " no reconocido.");
    } 
    public Symbol getS(){
        return this.s;
    }
:};

terminal 
    Identificador, OperadorComa, OperadorCorcheteDer, OperadorCorcheteIz, OperadorNegacion,
    OperadorDoblePunto, OperadorParentesisDer, OperadorParentesisIz, OperadorParCuadradoIz, 
    OperadorParCuadradoDer, OperadorPuntoComa, Char, Int, Long, Short, Entero, Flotante, 
    FlotanteExponente, Octal, OctalFlotante, Hexadecimal, HexadecimalFlotante, Caracter, 
    Const, Write, Read, For, While, Switch, If, Else, Void, Case, Default, Continue, Break, 
    OperadorIgual, OperadorMayorIgual, OperadorMayor, OperadorMenorIgual, OperadorMenor,
    OperadorDesigual, OperadorORLazy, OperadorANDLazy, OperadorXOR, OperadorOR, OperadorAND,
    OperadorMultiIgual, OperadorDivIgual, OperadorModIgual, OperadorIncremental, OperadorDecremental,
    OperadorMas, OperadorMenos, OperadorMultiplicacion, OperadorDivision, OperadorModulo, 
    OperadorMasIgual, OperadorMenosIgual, OperadorIgualIgual, Error
;

non terminal 
    INICIO, LITERAL, DECLARACION, DECLARACION_FUNCION, EXPRESION, DECLARADOR, INICIALIZADOR, DECLARACION_EXTERNA, 
    EXPRESION_PRIMARIA, EXPRESION_POSTFIJA, LISTA_ARGUMENTOS_EXPRESIONES, EXPRESION_UNARIA, EXPRESION_MULTIPLICATIVA, 
    EXPRESION_ADITIVA, EXPRESION_RELACIONAL, EXPRESION_IGUALDAD, EXPRESION_AND, EXPRESION_XOR, EXPRESION_OR, 
    EXPRESION_LAZY_AND, EXPRESION_LAZY_OR, EXPRESION_ASIGNACION, OPERADOR_ASIGNACION, LISTA_DECLARADORES_INICIALIZADORES, 
    ESPECIFICADORES_DECLARACION, DECLARADOR_INICIALIZADOR, ESPECIFICADOR_TIPO, LISTA_CUALIFICADOR_ESPECIFICADOR, 
    CUALIFICADOR_TIPO, LISTA_TIPOS_PARAMETROS, LISTA_PARAMETROS, DECLARACION_PARAMETRO, LISTA_IDENTIFICADORES, NOMBRE_TIPO,
    DECLARADOR_ABSTRACTO, DECLARADOR_ABSTRACTO_DIRECTO, LISTA_INICIALIZADORES, SENTENCIA, SENTENCIA_ETIQUETADA, 
    SENTENCIA_COMPUESTA, LISTA_DECLARACIONES, LISTA_SENTENCIAS, SENTENCIA_EXPRESION, SENTENCIA_SELECCION, SENTENCIA_ITERACION,
    SENTENCIA_SALTO, SENTENCIA_READ_WRITE, OPERADOR_UNARIO  
;

precedence left OperadorCorcheteIz, OperadorCorcheteDer, OperadorParentesisIz, OperadorParentesisDer, OperadorIncremental, OperadorDecremental;
precedence left OperadorNegacion;
precedence left OperadorMultiplicacion, OperadorDivision, OperadorModulo;
precedence left OperadorMas, OperadorMenos;
precedence left OperadorMenor, OperadorMayor, OperadorMenorIgual, OperadorMayorIgual;
precedence left OperadorIgualIgual, OperadorDesigual;
precedence left OperadorAND;
precedence left OperadorXOR;
precedence left OperadorOR;
precedence left OperadorANDLazy;
precedence left OperadorORLazy;
precedence right OperadorIgual, OperadorMultiIgual, OperadorDivIgual, OperadorModIgual, OperadorMasIgual, OperadorMenosIgual;

start with INICIO
;

INICIO 
    ::= DECLARACION_EXTERNA 
    |   INICIO DECLARACION_EXTERNA 
;

DECLARACION_EXTERNA 
    ::= DECLARACION 
    |   DECLARACION_FUNCION
;

DECLARACION_FUNCION
    ::= ESPECIFICADORES_DECLARACION Identificador OperadorParentesisIz LISTA_PARAMETROS OperadorParentesisDer SENTENCIA_COMPUESTA
    |   Void Identificador OperadorParentesisIz LISTA_PARAMETROS OperadorParentesisDer SENTENCIA_COMPUESTA
    |   ESPECIFICADORES_DECLARACION Identificador OperadorParentesisIz LISTA_IDENTIFICADORES OperadorParentesisDer SENTENCIA_COMPUESTA
    |   Void Identificador OperadorParentesisIz LISTA_IDENTIFICADORES OperadorParentesisDer SENTENCIA_COMPUESTA
    |   ESPECIFICADORES_DECLARACION Identificador OperadorParentesisIz OperadorParentesisDer SENTENCIA_COMPUESTA
    |   Void Identificador OperadorParentesisIz OperadorParentesisDer SENTENCIA_COMPUESTA
    |   error OperadorCorcheteDer {: errores.add(new ErrorSintactico(parser.getS(), "Declaración de función no válida")); :} 
;

EXPRESION_PRIMARIA 
    ::= Identificador:identificador {: Traductor.recuerdaVariable((String) identificador, parser.getS()) ; PilaSemantica.printArray(); :}
    |   LITERAL 
    |   OperadorParentesisIz EXPRESION OperadorParentesisDer
;

LITERAL 
    ::= Entero:e {: Traductor.recuerdaConstante((String) e) ; PilaSemantica.printArray(); :}
    |   Flotante:f {: Traductor.recuerdaConstante((String) f); PilaSemantica.printArray(); :}
    |   FlotanteExponente:fe {: Traductor.recuerdaConstante((String) fe); PilaSemantica.printArray(); :}
    |   Octal:o {: Traductor.recuerdaConstante((String) o); PilaSemantica.printArray(); :}
    |   OctalFlotante:oe {: Traductor.recuerdaConstante((String) oe); PilaSemantica.printArray(); :}
    |   Hexadecimal:h {: Traductor.recuerdaConstante((String) h); PilaSemantica.printArray(); :}
    |   HexadecimalFlotante:hf {: Traductor.recuerdaConstante((String) hf); PilaSemantica.printArray(); :}
    |   Caracter:c {: Traductor.recuerdaConstante((String) c); PilaSemantica.printArray(); :}
;

EXPRESION_POSTFIJA 
    ::= EXPRESION_PRIMARIA
    |   EXPRESION_POSTFIJA OperadorParCuadradoIz EXPRESION OperadorParCuadradoDer
    |   EXPRESION_POSTFIJA OperadorParentesisIz OperadorParentesisDer
    |   EXPRESION_POSTFIJA OperadorParentesisIz LISTA_ARGUMENTOS_EXPRESIONES OperadorParentesisDer
    |   EXPRESION_POSTFIJA OperadorIncremental
    |   EXPRESION_POSTFIJA OperadorDecremental
;

LISTA_ARGUMENTOS_EXPRESIONES 
    ::= EXPRESION_ASIGNACION 
    |   LISTA_ARGUMENTOS_EXPRESIONES OperadorComa EXPRESION_ASIGNACION
;

EXPRESION_UNARIA 
    ::= EXPRESION_POSTFIJA
    |   OperadorIncremental EXPRESION_UNARIA
    |   OperadorDecremental EXPRESION_UNARIA
    |   OPERADOR_UNARIO EXPRESION_UNARIA
;

OPERADOR_UNARIO 
    ::= OperadorAND
    |   OperadorMultiplicacion
    |   OperadorMas
    |   OperadorMenos
    |   OperadorNegacion
;

EXPRESION_MULTIPLICATIVA
    ::= EXPRESION_UNARIA 
    |   EXPRESION_MULTIPLICATIVA OperadorMultiplicacion EXPRESION_UNARIA
    |   EXPRESION_MULTIPLICATIVA OperadorDivision EXPRESION_UNARIA
    |   EXPRESION_MULTIPLICATIVA OperadorModulo EXPRESION_UNARIA
;

EXPRESION_ADITIVA
    ::= EXPRESION_MULTIPLICATIVA
    |   EXPRESION_ADITIVA OperadorMas EXPRESION_MULTIPLICATIVA
    |   EXPRESION_ADITIVA OperadorMenos EXPRESION_MULTIPLICATIVA
;

EXPRESION_RELACIONAL
    ::= EXPRESION_ADITIVA
    |   EXPRESION_RELACIONAL OperadorMenor EXPRESION_ADITIVA
    |   EXPRESION_RELACIONAL OperadorMayor EXPRESION_ADITIVA
    |   EXPRESION_RELACIONAL OperadorMenorIgual EXPRESION_ADITIVA
    |   EXPRESION_RELACIONAL OperadorMayorIgual EXPRESION_ADITIVA
;

EXPRESION_IGUALDAD
    ::= EXPRESION_RELACIONAL
    |   EXPRESION_IGUALDAD OperadorIgualIgual EXPRESION_RELACIONAL
    |   EXPRESION_IGUALDAD OperadorDesigual EXPRESION_RELACIONAL
;

EXPRESION_AND
    ::= EXPRESION_IGUALDAD
    |   EXPRESION_AND OperadorAND EXPRESION_IGUALDAD
;

EXPRESION_XOR
    ::= EXPRESION_AND
    |   EXPRESION_XOR OperadorXOR EXPRESION_AND
;

EXPRESION_OR
    ::= EXPRESION_XOR
    |   EXPRESION_OR OperadorOR EXPRESION_XOR
;

EXPRESION_LAZY_AND
    ::= EXPRESION_OR
    |   EXPRESION_LAZY_AND OperadorANDLazy EXPRESION_OR
;

EXPRESION_LAZY_OR
    ::= EXPRESION_LAZY_AND
    |   EXPRESION_LAZY_OR OperadorORLazy EXPRESION_LAZY_AND
;

EXPRESION_ASIGNACION
    ::= EXPRESION_LAZY_OR
    |   EXPRESION_UNARIA OPERADOR_ASIGNACION EXPRESION_ASIGNACION
;

OPERADOR_ASIGNACION 
    ::= OperadorIgual       
    |   OperadorMasIgual    
    |   OperadorMenosIgual  
    |   OperadorMultiIgual  
    |   OperadorDivIgual    
    |   OperadorModIgual    
;

EXPRESION
    ::= EXPRESION_ASIGNACION
    |   EXPRESION OperadorComa EXPRESION_ASIGNACION
    |   OperadorNegacion EXPRESION_ASIGNACION
;

DECLARACION 
    ::= ESPECIFICADORES_DECLARACION OperadorPuntoComa:OpPuntoComa {: System.out.println("Fin de declaración: "); PilaSemantica.printArray(); System.out.println("Insertando en ts: "); Traductor.insertarTS(parser.getS()); System.out.println("Tabla: " + TablaSimbolos.tabla); :}
    |   ESPECIFICADORES_DECLARACION LISTA_DECLARADORES_INICIALIZADORES OperadorPuntoComa {: System.out.println("Fin de declaración: "); PilaSemantica.printArray(); System.out.println("Insertando en ts: "); Traductor.insertarTS(parser.getS()); System.out.println("Tabla: " + TablaSimbolos.tabla); :}
    |   error OperadorPuntoComa {: errores.add(new ErrorSintactico(parser.getS(), "Declaración no válida")); :}
;

ESPECIFICADORES_DECLARACION 
    ::= ESPECIFICADOR_TIPO ESPECIFICADORES_DECLARACION
    |   ESPECIFICADOR_TIPO
    |   CUALIFICADOR_TIPO
    |   CUALIFICADOR_TIPO ESPECIFICADORES_DECLARACION
;

LISTA_DECLARADORES_INICIALIZADORES
    ::= DECLARADOR_INICIALIZADOR 
    |   LISTA_DECLARADORES_INICIALIZADORES OperadorComa DECLARADOR_INICIALIZADOR 
;

DECLARADOR_INICIALIZADOR 
    ::= DECLARADOR 
    |   DECLARADOR OperadorIgual INICIALIZADOR 
;

ESPECIFICADOR_TIPO 
    ::= Char:char_token {: Traductor.recuerdaTipo((String) char_token) ; PilaSemantica.printArray(); :}
    |   Int:int_token {: Traductor.recuerdaTipo((String) int_token) ; PilaSemantica.printArray(); :}
    |   Long:long_token {: Traductor.recuerdaTipo((String) long_token) ; PilaSemantica.printArray(); :}
    |   Short:short_token {: Traductor.recuerdaTipo((String) short_token) ; PilaSemantica.printArray(); :}
;

LISTA_CUALIFICADOR_ESPECIFICADOR
    ::= ESPECIFICADOR_TIPO LISTA_CUALIFICADOR_ESPECIFICADOR
    |   ESPECIFICADOR_TIPO
    |   CUALIFICADOR_TIPO LISTA_CUALIFICADOR_ESPECIFICADOR
    |   CUALIFICADOR_TIPO
;

CUALIFICADOR_TIPO
    ::= Const
;      

DECLARADOR 
    ::= Identificador:id {: Traductor.recuerdaId((String) id) ; PilaSemantica.printArray(); System.out.println("Encuentra: "+id); :} 
    |   OperadorParentesisIz DECLARADOR OperadorParentesisDer  
    |   DECLARADOR OperadorParCuadradoIz EXPRESION_LAZY_OR OperadorParCuadradoDer
    |   DECLARADOR OperadorParCuadradoIz OperadorParCuadradoDer
    |   DECLARADOR OperadorParentesisIz LISTA_TIPOS_PARAMETROS OperadorParentesisDer
    |   DECLARADOR OperadorParentesisIz LISTA_IDENTIFICADORES OperadorParentesisDer
    |   DECLARADOR OperadorParentesisIz OperadorParentesisDer
;

LISTA_TIPOS_PARAMETROS 
    ::= LISTA_PARAMETROS 
;

LISTA_PARAMETROS 
    ::= DECLARACION_PARAMETRO 
    |   LISTA_PARAMETROS OperadorComa DECLARACION_PARAMETRO
;

DECLARACION_PARAMETRO 
    ::= ESPECIFICADORES_DECLARACION DECLARADOR 
    |   ESPECIFICADORES_DECLARACION DECLARADOR_ABSTRACTO
    |   ESPECIFICADORES_DECLARACION
;

LISTA_IDENTIFICADORES
    ::= Identificador
    |   LISTA_IDENTIFICADORES OperadorComa Identificador
;

NOMBRE_TIPO 
    ::= LISTA_CUALIFICADOR_ESPECIFICADOR 
    |   LISTA_CUALIFICADOR_ESPECIFICADOR DECLARADOR_ABSTRACTO
;

DECLARADOR_ABSTRACTO
    ::= DECLARADOR_ABSTRACTO_DIRECTO
;

DECLARADOR_ABSTRACTO_DIRECTO
    ::= OperadorParentesisIz DECLARADOR_ABSTRACTO OperadorParentesisDer 
    |   OperadorParCuadradoIz OperadorParCuadradoDer 
    |   OperadorParCuadradoIz EXPRESION_LAZY_OR OperadorParCuadradoDer 
    |   DECLARADOR_ABSTRACTO_DIRECTO OperadorParCuadradoIz OperadorParCuadradoDer
    |   DECLARADOR_ABSTRACTO_DIRECTO OperadorParCuadradoIz EXPRESION_LAZY_OR OperadorParCuadradoDer
    |   OperadorParentesisIz OperadorParentesisDer
    |   OperadorParentesisIz LISTA_TIPOS_PARAMETROS OperadorParentesisDer
    |   DECLARADOR_ABSTRACTO_DIRECTO OperadorParentesisIz OperadorParentesisDer
    |   DECLARADOR_ABSTRACTO_DIRECTO OperadorParentesisIz LISTA_TIPOS_PARAMETROS OperadorParentesisDer
;

INICIALIZADOR 
    ::= EXPRESION_ASIGNACION
    |   OperadorCorcheteIz LISTA_INICIALIZADORES OperadorCorcheteDer  
    |   OperadorCorcheteIz LISTA_INICIALIZADORES OperadorComa OperadorCorcheteDer  

;

LISTA_INICIALIZADORES 
    ::= INICIALIZADOR
    |   LISTA_INICIALIZADORES OperadorComa INICIALIZADOR
;

SENTENCIA 
    ::= SENTENCIA_ETIQUETADA
    |   SENTENCIA_COMPUESTA
    |   SENTENCIA_EXPRESION
    |   SENTENCIA_SELECCION
    |   SENTENCIA_ITERACION
    |   SENTENCIA_SALTO
    |   SENTENCIA_READ_WRITE
    |   error OperadorCorcheteDer {: errores.add(new ErrorSintactico(parser.getS(), "Sentencia no válida")); :}
;

SENTENCIA_ETIQUETADA
    ::= Case EXPRESION_LAZY_OR OperadorDoblePunto SENTENCIA
    |   Default OperadorDoblePunto SENTENCIA
;

SENTENCIA_COMPUESTA
    ::= OperadorCorcheteIz OperadorCorcheteDer 
    |   OperadorCorcheteIz LISTA_SENTENCIAS OperadorCorcheteDer 
    |   OperadorCorcheteIz LISTA_DECLARACIONES OperadorCorcheteDer 
    |   OperadorCorcheteIz LISTA_DECLARACIONES LISTA_SENTENCIAS OperadorCorcheteDer 
;

LISTA_DECLARACIONES
    ::= DECLARACION 
    |   LISTA_DECLARACIONES DECLARACION
;

LISTA_SENTENCIAS
    ::= SENTENCIA
    |   LISTA_SENTENCIAS SENTENCIA
; 

SENTENCIA_EXPRESION 
    ::= OperadorPuntoComa 
    |   EXPRESION OperadorPuntoComa
;

SENTENCIA_SELECCION 
    ::= If OperadorParentesisIz EXPRESION OperadorParentesisDer SENTENCIA_COMPUESTA
    |   If OperadorParentesisIz EXPRESION OperadorParentesisDer SENTENCIA_COMPUESTA Else SENTENCIA_COMPUESTA
    |   Switch OperadorParentesisIz EXPRESION OperadorParentesisDer SENTENCIA_COMPUESTA
;

SENTENCIA_ITERACION 
    ::= While OperadorParentesisIz EXPRESION OperadorParentesisDer SENTENCIA_COMPUESTA
    |   For OperadorParentesisIz SENTENCIA_EXPRESION SENTENCIA_EXPRESION OperadorParentesisDer SENTENCIA_COMPUESTA
    |   For OperadorParentesisIz SENTENCIA_EXPRESION SENTENCIA_EXPRESION EXPRESION OperadorParentesisDer SENTENCIA_COMPUESTA
;

SENTENCIA_SALTO 
    ::= Continue OperadorPuntoComa
    |   Break OperadorPuntoComa
;

SENTENCIA_READ_WRITE 
    ::= Write OperadorParentesisIz EXPRESION OperadorParentesisDer OperadorPuntoComa
    |   Read OperadorParentesisIz OperadorParentesisDer OperadorPuntoComa
    |   Read OperadorParentesisIz Identificador OperadorParentesisDer OperadorPuntoComa
;